import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.17.4'
    id 'org.jetbrains.kotlin.jvm' version '2.0.20'
}

static def gitTag() {
    def tag = ""
    def proc = "git describe --tags".execute()
    proc.in.eachLine { line -> tag = line }
    proc.waitFor()

    if (tag ==~ /v\d+\.\d+\.\d+/)
        return [tag.substring(1), "default"]

    def branch = ""
    proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.waitFor()

    [branch, "branch"]
}

def (String ver, String channel) = gitTag()

group 'com.ypwang.plugin'
version ver

repositories {
    mavenCentral()
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = 'LATEST-EAP-SNAPSHOT'
    type = 'IU'
    plugins = ['org.jetbrains.plugins.go:243.16718.32']
    updateSinceUntilBuild = false
}

kotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

def changeLog() {
    def all = project.file('change-notes.htm').text
    def count = 0
    def idx = -1

    // keep at most 5 recent change logs
    while (count < 6) {
        idx = all.indexOf("<p>", idx+1)
        if (idx == -1)
            return all

        count++
    }

    return all.substring(0, idx) + """\n<a href="https://github.com/xxpxxxxp/intellij-plugin-golangci-lint/blob/master/change-notes.htm">Previous...</a>"""
}

patchPluginXml {
    changeNotes = changeLog()
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
    channels = [channel]
}

runIde {
    maxHeapSize = "4g"
}